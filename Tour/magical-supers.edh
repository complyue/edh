
{

  class MetaSuper1 {

    # handles magic method resolution on a magic child (referred to by `that`),
    # for:
    #    *) `this.xxx` (@<-)
    #    *) `this.xxx = yyy` (<-@)
    #    *) `obj.xxx` (@<-*)
    #    *) `obj.xxx = yyy` (*<-@)
    #    *) `extends xxx` (<-^)
    # where `this`, `obj` and procedure context this is a grand business child
    method (!<-) ( magicSpell ) {
      console.warn<| 'casting spell (' ++ magicSpell ++ ')'

      # note method binding is a concern need careful handling

      NA # do no override
    }

    # handles super attribute resolution, on a magic child (referred to by `that`),
    # for `super.xxx` from a grand business child
    method (@<-^) ( attrKey ) {
      console.info<| 'getting super.' ++ attrKey ++ ' from the super'

      # note method binding is a concern need careful handling

      NA # do no override
    }

  }

}

{

  class Super1 { extends MetaSuper1

    # handles `extends` from a business child (referred to by `that`),
    # `this` is the new super object being added to `that`, if this
    # method err out, the inheritance relationship won't be established.
    # `boScope` is a scope object reflecting `that` object's entity
    method (<-^) ( boScope ) {
      console.info<| 'connecting child of ' ++ constructor( that )
      ++ ' which is defined at: ' ++ boScope.lexiLoc
    }

    # handles `this.xxx` from a business child (referred to by `that`)
    method (@<-) ( attrKey ) {
      console.info<| 'getting this.' ++ attrKey
      return NA # try next super, or fail as normally if no such attr
    }

    # handles `obj.xxx` from a business child (referred to by `that`)
    method (@<-*) ( attrKey ) {
      console.info<| 'getting ' ++ attrKey ++ ' from the obj'
      that?@attrKey # giving out nil if no such attr
    }

    # a readonly property providing fixed default value
    property$
    method n() 23

    # handles `this.xxx = yyy` from a business child (referred to by `that`)
    method (<-@) ( attrKey, attrVal ) {
      console.info<| 'setting this.' ++ attrKey ++ ' to ' ++ attrVal
      that@attrKey = attrVal
    }

    # handles `obj.xxx = yyy` for `obj` being a business child (referred to by `that`)
    method (*<-@) ( attrKey, attrVal ) {
      console.info<| 'setting ' ++ attrKey ++ ' to ' ++ attrVal ++ ' for the obj'
      that@attrKey = attrVal
    }


  }

  class BusObj1 { extends Super1

    property$
    method n () {
      console.warn<| 'super provided n - ' ++ super?n
      this.n1
    }

    this.n1 = 7

    this.n2 = 3

  }

}

bo1 = BusObj1()

{

  console.info<| 'bo1.n is ' ++ bo1.n

  console.info<| 'bo1.n1 is ' ++ bo1.n1

  console.info<| 'bo1.n2 is ' ++ bo1.n2

  # destined to fail
  # console.info<| 'bo1.n3 is ' ++ bo1.n3

}

{
  bo1.n1 = 5
  console.info<| 'bo1.n1 set to ' ++ bo1.n1
}
